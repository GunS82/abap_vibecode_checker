REPORT RSTABLESIZE .
PARAMETER: CLIENT      LIKE T000-MANDT DEFAULT SY-MANDT,
           TABLE       LIKE DD02L-TABNAME,
           CLUSTCOM    TYPE OAX,
           NOCLASSL    TYPE OAX DEFAULT 'X',
           NOCLASSA    TYPE OAX.
TYPE-POOLS: SLIS.
DATA: FIELDCAT         TYPE SLIS_T_FIELDCAT_ALV,
      FIELDCAT_LINE    TYPE SLIS_FIELDCAT_ALV,
      SIZETAB          LIKE CCTABSIZE OCCURS 0 WITH HEADER LINE,
      DD02L_TAB        LIKE DD02L OCCURS 0 WITH HEADER LINE,
      T000_TAB         LIKE T000-MANDT OCCURS 0 WITH HEADER LINE,
      VRS_TAB          LIKE VRSTABC OCCURS 0 WITH HEADER LINE,
      REPID            LIKE SY-REPID,
      COUNT            TYPE I,
      BEGIN OF SUMTAB OCCURS 0,
        MANDT          LIKE CCCFLOW-MANDT,
        SIZE           LIKE CCTABSIZE-SIZE_KB,
      END OF SUMTAB.
DATA: TEXT(50),
      LENGTH           TYPE I,
      TABLE_ORI        TYPE TABNAME.
DATA: I                TYPE I,
      TABNAME          TYPE TABNAME.
DATA: S_LAYOUT         TYPE SLIS_LAYOUT_ALV.
DATA: BYPASS.

TABLE_ORI  = TABLE.


AUTHORITY-CHECK OBJECT 'S_TABU_CLI'
         ID 'CLIIDMAINT' FIELD 'X'.
IF SY-SUBRC <> 0.
  WRITE:
  'Sie haben nicht die Berechtigung, dieses Programm zu nutzen'(001).
  RETURN.
ENDIF.

REPLACE '*' WITH '%' INTO CLIENT.
REPLACE '*' WITH '%' INTO TABLE.
DELETE FROM EUINFO WHERE OBJECT = 'TABL' AND OBJ_NAME = 'CCTABSIZE'.
SELECT * FROM DD02L INTO TABLE DD02L_TAB WHERE TABNAME LIKE TABLE
                    AND   AS4LOCAL = 'A'
                    AND   CLIDEP   = 'X'
                    AND ( TABCLASS EQ 'TRANSP'
                    OR    TABCLASS EQ 'POOL'
                    OR    TABCLASS EQ 'CLUSTER' ).

IF CLUSTCOM = 'X'.
  LOOP AT DD02L_TAB WHERE TABCLASS EQ 'CLUSTER'.
    I = SY-TABIX + 1.
    DD02L_TAB-TABNAME =  DD02L_TAB-SQLTAB.
    CLEAR DD02L_TAB-SQLTAB.
    CLEAR DD02L_TAB-CONTFLAG.
    DD02L_TAB-TABCLASS = 'TABCLUST'.
    MODIFY DD02L_TAB.
    TABNAME = DD02L_TAB-TABNAME.
    LOOP AT DD02L_TAB FROM I                      "#EC CI_NOORDER
                      WHERE TABCLASS EQ 'CLUSTER'
                        AND SQLTAB = TABNAME.
      DELETE DD02L_TAB.
    ENDLOOP.
  ENDLOOP.
ENDIF.

SELECT MANDT FROM T000 INTO TABLE T000_TAB WHERE MANDT LIKE CLIENT.
IF SY-SUBRC <> 0.
  T000_TAB = CLIENT.
  APPEND T000_TAB.
ENDIF.
SELECT * FROM VRSTABC INTO TABLE VRS_TAB
                            WHERE TABMODE = 'X'
                              AND MAXRELEASE >= SY-SAPRL. "#EC PORTABLE
SORT VRS_TAB.

SORT DD02L_TAB.
DELETE ADJACENT DUPLICATES FROM DD02L_TAB COMPARING TABNAME.

LOOP AT DD02L_TAB.
  IF DD02L_TAB-CONTFLAG <> 'L' OR NOCLASSL = ' '.
    IF DD02L_TAB-CONTFLAG <> 'A' OR NOCLASSA = ' '.
      CLEAR VRS_TAB.
      READ TABLE VRS_TAB WITH KEY TABNAME = DD02L_TAB-TABNAME
                                  BINARY SEARCH.
      IF SY-SUBRC <> 0.
        SIZETAB-TABNAME  = DD02L_TAB-TABNAME.
        SIZETAB-CLASS    = DD02L_TAB-TABCLASS.
        SIZETAB-CONTFLAG = DD02L_TAB-CONTFLAG.
        IF SIZETAB-CLASS = 'TABCLUST'.
          SELECT SINGLE DEVCLASS INTO SIZETAB-DEVCLASS FROM TADIR
                           WHERE PGMID    = 'R3TR'        AND
                                 OBJECT   = 'SQLT'        AND
                                 OBJ_NAME = SIZETAB-TABNAME.
        ELSE.
          SELECT SINGLE DEVCLASS INTO SIZETAB-DEVCLASS FROM TADIR
                           WHERE PGMID    = 'R3TR'        AND
                                 OBJECT   = 'TABL'        AND
                                 OBJ_NAME = SIZETAB-TABNAME.
        ENDIF.
        IF NOCLASSL = 'X' AND SIZETAB-DEVCLASS(1) = '$'.
          CONTINUE.
        ENDIF.
        LOOP AT T000_TAB.
          SIZETAB-MANDT   = T000_TAB.
          PERFORM GET_TABLE_SIZE USING SIZETAB.
          APPEND SIZETAB.
          COUNT = COUNT + 1.
          IF COUNT = 100.
            CLEAR COUNT.
            COMMIT WORK.
          ENDIF.
        ENDLOOP.
      ENDIF.
    ENDIF.
  ENDIF.
ENDLOOP.

REPID = SY-REPID.
CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
  EXPORTING
    I_PROGRAM_NAME         = REPID
    I_INTERNAL_TABNAME     = 'SIZETAB'
    I_INCLNAME             = REPID
    I_BYPASSING_BUFFER     = BYPASS
  CHANGING
    CT_FIELDCAT            = FIELDCAT
  EXCEPTIONS
    INCONSISTENT_INTERFACE = 1
    PROGRAM_ERROR          = 2
    OTHERS                 = 3.
IF SY-SUBRC <> 0.
  MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
          WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
ENDIF.
LOOP AT FIELDCAT INTO FIELDCAT_LINE WHERE FIELDNAME(4) = 'SIZE'.
  CLEAR FIELDCAT_LINE-REF_TABNAME.
  IF FIELDCAT_LINE-FIELDNAME = 'SIZE_KB'.
    CLEAR FIELDCAT_LINE-REF_TABNAME.
  ELSE.
    FIELDCAT_LINE-NO_OUT = 'X'.
  ENDIF.
  MODIFY FIELDCAT FROM FIELDCAT_LINE.
ENDLOOP.
LOOP AT FIELDCAT INTO FIELDCAT_LINE WHERE FIELDNAME = 'TABNAME'.
  FIELDCAT_LINE-FIX_COLUMN = 'X'.
  MODIFY FIELDCAT FROM FIELDCAT_LINE.
ENDLOOP.

* Show data with ALV **************************************************
CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
  EXPORTING
    IT_FIELDCAT             = FIELDCAT
    I_CALLBACK_PROGRAM      = 'RSTABLESIZE'
    I_CALLBACK_USER_COMMAND = 'ALV_USER_COMM'
    IS_LAYOUT               = S_LAYOUT
    I_BYPASSING_BUFFER      = BYPASS
  TABLES
    T_OUTTAB                = SIZETAB
  EXCEPTIONS
    PROGRAM_ERROR           = 1
    OTHERS                  = 2.
IF SY-SUBRC <> 0.
  WRITE: 'SY-SUBRC: ', SY-SUBRC, 'REUSE_ALV_LIST_DISPLAY'.  "#EC NOTEXT
ENDIF.

LOOP AT SIZETAB.
  MOVE SIZETAB-MANDT   TO SUMTAB-MANDT.
  MOVE SIZETAB-SIZE_KB TO SUMTAB-SIZE.
  COLLECT SUMTAB.
ENDLOOP.
SORT SUMTAB BY SIZE DESCENDING.
CLEAR SIZETAB-SIZE.

WRITE :/ 'Tabellenanalyse Stand:'(700).
WRITE : SY-DATUM, SY-UZEIT.
ULINE.

WRITE:/ 'Tabelle:'(702), TABLE_ORI.
TEXT = 'Nicht Auslieferungsklasse &:'(701).
WRITE:/ 'Cluster komprimieren:'(704), AT 42 CLUSTCOM.
REPLACE '&' WITH 'L' INTO TEXT.
LENGTH = STRLEN( TEXT ) + 1.
WRITE:/ TEXT(LENGTH), AT 42 NOCLASSL.
TEXT = 'Nicht Auslieferungsklasse &:'(701).
REPLACE '&' WITH 'A' INTO TEXT.
WRITE:/ TEXT(LENGTH), AT 42 NOCLASSA.

ULINE.
LOOP AT SUMTAB.
  WRITE: / 'Mandant:'(703), SUMTAB-MANDT,
         AT 53 SUMTAB-SIZE, 'kByte'.                        "#EC NOTEXT
  SIZETAB-SIZE = SIZETAB-SIZE + SUMTAB-SIZE.
ENDLOOP.
IF SY-TFILL > 1.
  ULINE.
  WRITE:/ 'Summe:'(003), 53 SIZETAB-SIZE, 'kByte'.          "#EC NOTEXT
ENDIF.

*&---------------------------------------------------------------------*
*&      Form  GET_TABLE_SIZE
*&---------------------------------------------------------------------*
FORM GET_TABLE_SIZE USING    P_SIZETAB STRUCTURE CCTABSIZE.

  DATA: TEXT(80),
        X030L_TAB      LIKE X030L,
        NT_SUBRC       LIKE SY-SUBRC,
        SUBRC          LIKE SY-SUBRC.
  DATA: BEGIN  OF DFIES OCCURS 20.
          INCLUDE STRUCTURE DFIES.
  DATA: END OF DFIES.
  DATA: BEGIN  OF CTAB OCCURS 20,
         LINE(72).
  DATA: END OF CTAB.

  CALL FUNCTION 'SCCR_GET_NAMETAB_AND_TABLEN'
    EXPORTING
      TABNAME    = P_SIZETAB-TABNAME
    IMPORTING
      X030L      = X030L_TAB
      TABLEN     = P_SIZETAB-TABLEN
    TABLES
      DFIES      = DFIES
    EXCEPTIONS
      DDIC_ERROR = 1
      OTHERS     = 2.
  NT_SUBRC = SY-SUBRC.
  IF X030L_TAB-TABFORM CO 'PC'.
    CALL FUNCTION 'DB_EXISTS_TABLE'
      EXPORTING
        TABNAME = X030L_TAB-REFNAME
      IMPORTING
        SUBRC   = SUBRC.
  ELSE.
    CALL FUNCTION 'DB_EXISTS_TABLE'
      EXPORTING
        TABNAME = P_SIZETAB-TABNAME
      IMPORTING
        SUBRC   = SUBRC.
  ENDIF.

  IF SUBRC = 0 AND NT_SUBRC = 0.
    READ TABLE DFIES INDEX 1.
    TEXT = '& = ''&'' '.
    REPLACE '&' WITH DFIES-FIELDNAME INTO TEXT.
    REPLACE '&' WITH P_SIZETAB-MANDT INTO TEXT.
    CTAB-LINE = TEXT.
    APPEND CTAB.
    SELECT COUNT(*) INTO P_SIZETAB-NBRINS FROM (P_SIZETAB-TABNAME)
                                 CLIENT SPECIFIED WHERE (CTAB).
  ENDIF.
  P_SIZETAB-SIZE = P_SIZETAB-NBRINS * P_SIZETAB-TABLEN.
  P_SIZETAB-SIZE_KB = P_SIZETAB-SIZE / 1024.
ENDFORM.                               " GET_p_sizetab-tabname_SIZE

*&---------------------------------------------------------------------*
*&      ALV_Form  USER_COMM
*&---------------------------------------------------------------------*
FORM ALV_USER_COMM USING R_UCOMM     LIKE SY-UCOMM
                         RS_SELFIELD TYPE SLIS_SELFIELD.
  READ TABLE SIZETAB INDEX RS_SELFIELD-TABINDEX.
  IF SY-SUBRC = 0.
    IF SIZETAB-CLASS = 'TABCLUST'.
      CALL FUNCTION 'RS_DD_SQLT_EDIT'
        EXPORTING
          OBJNAME              = SIZETAB-TABNAME
          EDIT_MODE            = 'S'
        EXCEPTIONS
          OBJECT_NOT_FOUND     = 1
          OBJECT_NOT_SPECIFIED = 2
          PERMISSION_FAILURE   = 3
          NOT_EXECUTED         = 4
          OTHERS               = 5.
      IF SY-SUBRC <> 0.
        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
      ENDIF.
    ELSE.
      CALL FUNCTION 'RS_DD_SHOW'
        EXPORTING
          OBJNAME              = SIZETAB-TABNAME
          OBJTYPE              = 'T'
        EXCEPTIONS
          OBJECT_NOT_FOUND     = 1
          OBJECT_NOT_SPECIFIED = 2
          PERMISSION_FAILURE   = 3
          TYPE_NOT_VALID       = 4
          OTHERS               = 5.
      IF SY-SUBRC <> 0.
        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
      ENDIF.
    ENDIF.
  ENDIF.
ENDFORM.             